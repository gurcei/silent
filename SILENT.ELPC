#output "silent"

' BANK4 Memory usage
' ------------------
' 4.0000 DMA-LIST
' 4.0020 var interface to assembly
'        0020: ptr to default_red&
'        0022: ptr to default_green&
'        0024: ptr to default_blue&
'        0026: ptr to curr_red&
'        0028: ptr to curr_green&
'        002a: ptr to curr_blue&
'        002c: ptr to data_addr%
'        002e: ptr to cdata_addr%
'        0030: ptr to scraddr%
'        0032: ptr to clraddr%
'        0034: ptr to inc%
'        0036: ptr to starx%
'        0038: ptr to starvx%
' 4.0800 SCREEN MEMORY
' 4.2000 PETSCII GRAPHICS for BAS logo

' BANK5 Memory usage
' ------------------
' 5.0000 screen buffer for draw_petscii_frame
' 5.1000 color ram buffer for draw_petscii_frame
' 5.2000 screen buffer2 for 'anathema' layer
' 5.4000 color ram buffer2 for 'anathema' layer
' 5.3000 screen buffer3 for 'the silent enigma' layer
' 5.4000 color ram buffer3 for 'the silent enigma' layer


'--------
.declares
'--------
#declare starx%(20), stary(20), starvx%(20)
#declare k

#declare fname$, val&
#declare ldata&(2, 12288), didx%(2)=0, pidx%(2)=0, fidx%=0
#declare group_flag&, val&, rev_flag&(2) = 0
#declare offs(2), ver(2)
#declare lastflag, last_group_idx
#declare oldx, oldy, oldoffs(2), x, y, a$, xx, yy, dxx, dyy, debug, z$
#declare wxx, wyy

#declare chr, tmp

#declare src_addr, dest_addr, length, transp

#declare loc, ptr, m, tstart, addr

#declare char_cnt, char_idx%(60), char_width%(60), max_width
#declare txt_k, s$, ret

#declare mn_state = 0, wc_flag, wc_chr

#declare qlidx, qlcnt
#declare qlx1(10), qly1(10), qlx2(10), qly2(10), qlx3(10), qly3(10)
#declare qlx4(10), qly4(10)
#declare qlvx1, qlvy1, qlvx2, qlvy2
#declare qlclr(10), qlcurclr, kk, t, t1, t2, tx, ty, sides, sqinc
#declare x1, y1, x2, y2, sqang

#declare diffx, diffy, deltax, deltay, tanval, angle
#declare centrex, centrey, centrez
#declare cb_anga, cb_angb, cbidx, cbtmp, pz
#declare cb_inca, cb_incb, ret_ang, zfact
#declare px(10), py(10), plen, pang, pidx, pdist

#declare PI = 3.141592654269

.BASLOGO.EL_defines
'==================
  ' palette
  ' -------
  #declare default_red&(32), bkp_red&(32)
  #declare default_green&(32), bkp_green&(32)
  #declare default_blue&(32), bkp_blue&(32)
  #declare curr_red&(32)
  #declare curr_green&(32)
  #declare curr_blue&(32)
  #declare fadeval, tempval

#declare logo_state, frameidx, fx, fy, mirror, bas_state
#declare frmy(3), orig_frmy(3), finish_cnt, bcnt
#declare inc(3), inc_dir(3), data_addr%, cdata_addr%
#declare scraddr%, clraddr%, inc%, max_frames, frm_idx
#declare frame_addr%(25), frmx, frmy, frmw, frmh, frmx1, frmy1
#declare sz, flip_var, upper_nibble, lower_nibble

#declare rain_idx, plen, rainbowstr$, rainbow_x, rainbow_y
#declare subtext$(2), subtext_idx = 0
subtext$(0) = "presents..."
subtext$(1) = "a song by..."


.music_vars
'----------
#define chunk_max = 50
#declare chunk_cnt, chunk_idx, vidx, tmpo%, row
#declare echo_m, echo_b, echo_sz = .05, chunk_rpt, echo_delay
#declare v$(5,chunk_max), v
#declare song$
#declare v1$, v2$, v3$, v4$, v5$, v6$
#declare curr_freq, curr_dir

#declare seqcnt
#declare seq_chunk(200), seq_extra(200)

#declare plyptr, plyflag, ret%, tmp$

#define CMD_TEMPO = -1
#define CMD_FILTER = -2
#define CMD_LOOP = -3
#define CMD_ECHO_M1 = -4   ' echo melody: voice 1 on voice 4
#define CMD_ECHO_M2 = -5   ' echo melody: voice 1 on voice 2 + 4 + 5
#define CMD_ECHO_B  = -6   ' echo bass:   voice 3 on voice 6
#define CMD_ECHO_OFF = -7
#define CMD_ECHO_SZ = -8
#define CMD_VOL = -9
#define CMD_DELAY = -10
#define CMD_MAX = 11


#define FRM_B = 0
#define FRM_A = 1
#define FRM_S = 2
#define FRM_BACKDROP = 3

.offsets
#define OFFSET_DATA_SIZE  = 0
#define OFFSET_NUM_FRAMES = 2
#define OFFSET_FRAME0     = 3

#define FRM_OFFSET_X          = 0
#define FRM_OFFSET_Y          = 1
#define FRM_OFFSET_W          = 2
#define FRM_OFFSET_H          = 3
#define FRM_OFFSET_HITBOXES   = 4
#define FRM_OFFSET_HB_HEAD    = 4
#define FRM_OFFSET_HB_TORSO   = 8
#define FRM_OFFSET_HB_FEET    = 12
#define FRM_OFFSET_HB_ATTACK  = 16
#define FRM_OFFSET_DATA       = 20


'-------
.defines
'-------
#define OP_COPY = 0
#define OP_MIX  = 1
#define OP_SWAP = 2
#define OP_FILL = 3

#define DST_DEC = 32
#define SRC_DEC = 16

#define ST_FADE_TO_WHITE = 0
#define ST_DRAW_BACKDROP_ONLY = 1
#define ST_FADE_TO_DEFAULT = 2
#define ST_DROP_IN_B = 3
#define ST_PRESENTS = 4
#define ST_FADE_TO_STARS = 5
#define ST_DRAW_ANATHEMA = 6
#define ST_DRAW_SONG_TITLE = 7
#define ST_SHOW_QIX_LINES = 8
#define ST_SHOW_LYRICS =9


'----
.main
'----
  gosub init
  gosub state_loop
  end

'-------------
.iter_maindemo
'-------------

  if mn_state = 0 then begin
    s$ = "In dreamland..."
    txt_k = 1

    mn_state = 1
  bend

  if mn_state = 1 then begin
    fidx% = 2
    gosub iter_write_text
  bend

  if plyptr = -1 and rplay(1)=0 then print "{x93}the end!":end

  return


'----------
.state_loop
'----------
  tstart = ti

  if logo_state = ST_FADE_TO_WHITE then begin
    gosub fade_to_white
    logo_state = ST_DRAW_BACKDROP_ONLY

  bend
  
  if logo_state = ST_DRAW_BACKDROP_ONLY then begin
    bank 128
    poke $d020, 1, 1
    gosub init_80x50
    gosub draw_backdrop
    gosub page_flip
    gosub store_curr_colours
    gosub set_palette_for_backdrop
    fadeval=0
    m=0
    logo_state = ST_FADE_TO_DEFAULT
  bend
  
  if logo_state = ST_FADE_TO_DEFAULT then begin
    if fadeval <= 128 then begin
      ' fade_colours_to_defaults
      bank 0:vsync 10:sys $7e03, fadeval:bank 128
      fadeval = fadeval + 1
    bend

    ' cycle_alt_palette
    bank 0:sys $7e06:bank 128
    m = m + 1

    if m = 130 then logo_state = ST_DROP_IN_B
  bend

  if logo_state = ST_DROP_IN_B then begin
    gosub animate_logo
    ' cycle_alt_palette
    bank 0:sys $7e06:bank 128
    m = m + 1

    if m > 250 then logo_state = ST_PRESENTS:fadeval = 0
  bend

  if logo_state = ST_PRESENTS then begin
    gosub anim_draw_subtext
    ' cycle_alt_palette
    bank 0:sys $7e06:bank 128
    m = m + 1

    if m > 360 then begin
      ' fade to black
      bank 0:sys $7e0c, fadeval:bank 128
      fadeval = fadeval + 1
    bend

    if fadeval = 128 then begin
      poke $d020, 0, 0
      print "{x93}";
      gosub store_curr_colours
      gosub restore_default_palette
      logo_state = ST_FADE_TO_STARS
      fadeval = 0
    bend
  bend

  if logo_state = ST_FADE_TO_STARS then begin
      if fadeval <= 128 then begin
        ' fade_colours_to_defaults
        bank 0:vsync 10:sys $7e03, fadeval:bank 128
        fadeval = fadeval + 1
      bend
      gosub draw_starfield
      gosub page_flip
      gosub iter_starfield

      if fadeval = 129 then logo_state = ST_DRAW_ANATHEMA : m = 0
  bend

  if logo_state = ST_DRAW_ANATHEMA then begin
    ' gosub iter_maindemo
    m = m + 1
    gosub draw_starfield

    ' draw anathema logo
    fidx%=0
    gosub play_data_iter  ' line data

    gosub page_flip
    gosub iter_starfield

    if m = 720 then begin
      logo_state = ST_DRAW_SONG_TITLE
      m = 0
      offs(1) = -25
    bend
  bend

  if logo_state = ST_DRAW_SONG_TITLE then begin
    m = m + 1
    gosub draw_starfield

    ' scroll off anathema
    if m > 150 and m < 230 then fidx%=0:gosub scroll_canvas_right

    ' draw song title
    if m > 10 and m < 1900 then begin
      fidx%=1
      gosub play_data_iter
      gosub play_data_iter
      if mod(m, 4) = 0 then gosub scroll_canvas_right
      ' if m > 1910 then gosub scroll_canvas_right
    bend

    if plyptr >= 9 then gosub draw_qix_lines

    gosub page_flip
    gosub iter_starfield
  bend

  do
    ret% = 0
    do while ret% = 0
      gosub parse_current_seqptr
    loop
  loop while (ti-tstart) < .03

  goto state_loop

  return


'----------
.init_80x50
'----------
  ' switch back to upper-case
  poke $d018, peek($d018) and $fd

  ' switch to 80x50
  ' setbit HOTREG_7, 7
  ' poke $d031, $e8 ' 1110 1000 = H640,FAST,ATTR,V400
  print chr$(27);"5";

  ' wpoke SCRNPTRLSB, $0800
  ' poke SCRNPTRBNK, $04

  return


'-------------
.draw_backdrop
'-------------
    frameidx = FRM_BACKDROP
    fx=-999 : fy = -999
    mirror = 0
    gosub draw_petscii_frame
    return


'------------
.animate_logo
'------------
  if bas_state = 0 then begin
    orig_frmy(0) = 6
    orig_frmy(1) = 23
    orig_frmy(2) = 6

    frmy(0) = -44
    frmy(1) = -69
    frmy(2) = -107
    bas_state = 1

    inc_dir(0) = 1
    inc(0) = 1

    inc_dir(1) = 1
    inc(1) = 1

    inc_dir(2) = 1
    inc(2) = 1

    inc_dir(3) = 1
    inc(3) = 1

  bend

  if bas_state = 1 then begin
    finish_cnt = 0
    for k = 0 to 2
      if frmy(k) < orig_frmy(k) then begin
        frmy(k) = frmy(k) + 2
        if frmy(k) > orig_frmy(k) then frmy(k) = orig_frmy(k)
      bend

      if frmy(k) = orig_frmy(k) then finish_cnt = finish_cnt or 2^k
    next k

  bend
  gosub draw_bas
  gosub page_flip

  for frameidx = 0 to 2
    if finish_cnt and 2^frameidx and bcnt(frameidx)<3 then begin
      inc(frameidx) = inc(frameidx) + .2 * inc_dir(frameidx)
      if inc(frameidx) < .6 then begin
        inc_dir(frameidx) = 1
        bcnt(frameidx)=bcnt(frameidx)+1
      bend
      if inc(frameidx) > 2.9 then begin
        inc_dir(frameidx) = -1
        bcnt(frameidx)=bcnt(frameidx)+1
      bend

      if bcnt(frameidx) = 2 and inc(frameidx) > 1 then begin
        inc(frameidx) = 1
        bcnt(frameidx)=bcnt(frameidx)+1
      bend
    bend

    bend
  next frameidx
  
  return


'------------
.init_baslogo
'------------
  bank 0:bload "asmhelper":bank 128

  logo_state = 0

  gosub store_default_colours

  inc_dir(3) = -1
  inc(3) = 1

  ' clear dma list area

  for k = 0 to 20
    poke $40000+k, 0
  next k

  poke $40000, $81 ' opt = dest addr MB selector
  poke $40001, $00 ' default to 1st megabyte

  poke $40002, $07 ' opt = enable transparency
  poke $40003, $86 ' opt = set transparency
  poke $40004, $20 ' transparency value
  poke $40005, $00 ' end of options

  poke $40006, $00 ' CMD lsb = COPY
  wpoke $40007, $0000 ' length
  wpoke $40009, $1000 ' source addr
  poke $4000b, $05    ' source bank
  wpoke $4000c, $1000 ' dest addr
  poke $4000e, $05    ' dest bank
  poke $4000f, $00    ' CMD msb (ignore)
  wpoke $40010, $0000 ' modulo (ignore)

  ' init var interface to assembly
  loc = $40020

  ptr = pointer(default_red&(0))
  gosub store_ptr

  ptr = pointer(default_green&(0))
  gosub store_ptr

  ptr = pointer(default_blue&(0))
  gosub store_ptr

  ptr = pointer(curr_red&(0))
  gosub store_ptr

  ptr = pointer(curr_green&(0))
  gosub store_ptr

  ptr = pointer(curr_blue&(0))
  gosub store_ptr

  ptr = pointer(data_addr%)
  gosub store_ptr

  ptr = pointer(cdata_addr%)
  gosub store_ptr

  ptr = pointer(scraddr%)
  gosub store_ptr

  ptr = pointer(clraddr%)
  gosub store_ptr

  ptr = pointer(inc%)
  gosub store_ptr
  return


'---------
.load_data
'---------
  ptr = $42000
  
  bload "bas.bin",p(ptr)
  gosub init_petscii_frames
  gosub _increment_ptr_to_next_free
  return


'-------------------
.init_petscii_frames
'-------------------
  tmp = ptr
  max_frames = peek(ptr + OFFSET_NUM_FRAMES)
  'print
  'print "ptr=";hex$(ptr + OFFSET_NUM_FRAMES)
  ptr = ptr + OFFSET_FRAME0

  'print
  'print "- mx_frm=";max_frames;": "

  frm_idx = 0
  do while frm_idx < max_frames
    'print frm_idx;
    frame_addr%(frm_idx) = mod(ptr,65536)
    frmx = peek(ptr + FRM_OFFSET_X)
    frmy = peek(ptr + FRM_OFFSET_Y)
    frmw = peek(ptr + FRM_OFFSET_W)
    frmh = peek(ptr + FRM_OFFSET_H)

    frmx1=frmx+frmw-1
    frmy1=frmy+frmh-1

    data_addr% = mod(ptr, 65536)
    data_addr% = data_addr% + FRM_OFFSET_DATA
    cdata_addr% = data_addr% + frmw * frmh

    bank 4
    for y = 0 to frmy1-frmy
      for x = 0 to frmx1-frmx
        if peek(data_addr% + x + y * frmw) = 102 then begin
          poke (cdata_addr% + x + y * frmw), $ff
        bend
      next x
    next y
    bank 128
    
    'print "ptr=";hex$(ptr);", idx=";frm_idx;", w=";frmw;", h=";frmh
    ptr = ptr + FRM_OFFSET_DATA + frmw * frmh * 2

    'print "frm_idx=";frm_idx

    frm_idx = frm_idx + 1
  loop

  ptr = tmp
  return


'---------------------------
._increment_ptr_to_next_free
'---------------------------
  ptr = tmp
  sz = wpeek(ptr + OFFSET_DATA_SIZE)
  'print
  'print "inc sz = ";sz
  'print "ptr = ";hex$(ptr + OFFSET_DATA_SIZE)
  ptr = ptr + sz

  'get key a$
  return


'--------
.draw_bas
'--------
  frameidx = FRM_BACKDROP
  fx=-999 : fy = -999
  mirror = 0
  gosub draw_petscii_frame

  frameidx = FRM_B
  fx=-999 : fy=frmy(0)
  mirror = 0
  gosub draw_petscii_frame

  frameidx = FRM_A
  fx=-999 : fy=frmy(1)
  mirror = 0
  gosub draw_petscii_frame

  frameidx = FRM_S
  fx=-999 : fy=frmy(2)
  mirror = 0
  gosub draw_petscii_frame

  return


'------------------
.draw_petscii_frame
'------------------
  poke $4000b, $04  ' assure source bank 4 (for original frame data)
  if mirror = 1 then setbit $40006, 5:else clrbit $40006, 5
  data_addr% = frame_addr%(frameidx)
  bank 4
  frmx = peek(data_addr% + FRM_OFFSET_X)
  frmy = peek(data_addr% + FRM_OFFSET_Y)
  frmw = peek(data_addr% + FRM_OFFSET_W)
  frmh = peek(data_addr% + FRM_OFFSET_H)
  bank 128

  if fx<>-999 then frmx = fx ' override position
  if fy<>-999 then frmy = fy

  frmx1=frmx+frmw-1
  frmy1=frmy+frmh-1

  scraddr% = $0000 + frmx + (frmy+frmh-1) * 80
  clraddr% = $1000 + frmx + (frmy+frmh-1) * 80
  if mirror = 1 then clraddr% = clraddr% + frmw - 1

  data_addr% = data_addr% + FRM_OFFSET_DATA
  cdata_addr% = data_addr% + frmw * frmh
  inc% = inc(frameidx) * 256

  ' asm: draw_petscii_frame
  bank 0:sys $7e09, mod(frmy+256,256), mod(frmy1+256,256), frmw

  return


.fade_to_white
'-------------
  for fadeval=0 to 128
    ' fade_colours_to_white
    bank 0:vsync 10:sys $7e00, fadeval:bank 128

    do while ret% = 0
      gosub parse_current_seqptr
    loop
  next fadeval
  return


.store_default_colours
'---------------------
  for k = 0 to 31
    default_red&(k)=peek($d100+k)
    default_green&(k)=peek($d200+k)
    default_blue&(k)=peek($d300+k)
  next k
  return


.store_curr_colours
'------------------
  for k = 0 to 31
    curr_red&(k)=peek($d100+k)
    curr_green&(k)=peek($d200+k)
    curr_blue&(k)=peek($d300+k)
  next k
  return


'------------------------
.set_palette_for_backdrop
'------------------------
#define COL1_R = $da ' 218
#define COL1_G = $89 ' 137
#define COL1_B = $00 ' 0

#define COL2_R = $1b ' 27
#define COL2_G = $42 ' 66
#define COL2_B = $0b ' 11

  ' preserve original palette somewhere
  for k = 0 to 31
    bkp_red&(k) = default_red&(k)
    bkp_green&(k) = default_green&(k)
    bkp_blue&(k) = default_blue&(k)
  next k

  for k = 12 to 21
    flip_var = COL1_R + (COL2_R-COL1_R) * (k-12)/9
    gosub flip_nibble
    default_red&(k) = flip_var
    default_red&(43-k) = flip_var

    flip_var = COL1_G + (COL2_G-COL1_G) * (k-12)/9
    gosub flip_nibble
    default_green&(k) = flip_var
    default_green&(43-k) = flip_var

    flip_var = COL1_B + (COL2_B-COL1_B) * (k-12)/9
    gosub flip_nibble
    default_blue&(k) = flip_var
    default_blue&(43-k) = flip_var
  next k
  return


'-----------------------
.restore_default_palette
'-----------------------
  for k = 0 to 31
    default_red&(k) = bkp_red&(k)
    default_green&(k) = bkp_green&(k)
    default_blue&(k) = bkp_blue&(k)
  next k
  return

'-----------
.flip_nibble
'-----------
  lower_nibble = flip_var and $0f
  upper_nibble = (flip_var >> 4) and $0f

  flip_var = (lower_nibble << 4) + upper_nibble
  return


.anim_draw_subtext
'-----------------
  if m = 330 then begin
    subtext_idx = 1
    plen = 0
    edma 3,80,32,$40800+80*46
  bend

  if plen < len(subtext$(subtext_idx)) then begin
    plen = plen + .25
    rainbowstr$=left$(subtext$(subtext_idx),plen)
    if subtext_idx = 0 then rainbow_x = 35:else rainbow_x = 33
    rainbow_y = 46
    gosub print_rainbow_str
  bend
  return


.print_rainbow_str
'-----------------
  cursor rainbow_x, rainbow_y
  for rain_idx=1 to len(rainbowstr$)
    foreground mod(rain_idx-1,16)+16
    print mid$(rainbowstr$,rain_idx,1);
  next rain_idx
  return


'--------------
.draw_qix_lines
'--------------
  for k = 0 to qlcnt - 1
    kk = mod(qlidx + k, 10)

    sys $7e24, qlclr(kk) + 64

    x1 = qlx1(kk)
    y1 = qly1(kk)
    x2 = qlx2(kk)
    y2 = qly2(kk)

    sys $7e21, x1, y1, x2, y2

    ' gosub draw_line
  next k

  ' calc next line positions
  kk = qlidx  ' old qlidx
  qlidx = mod(qlidx + 1, 10)
  if qlcnt < 10 then qlcnt = qlcnt + 1

  qlx1(qlidx) = qlx1(kk) + qlvx1
  if qlx1(qlidx) >= 160 and qlvx1 > 0 then qlx1(qlidx)=159 : qlvx1 = -qlvx1
  if qlx1(qlidx) < 0 and qlvx1 < 0 then qlx1(qlidx)=0 : qlvx1 = -qlvx1

  qly1(qlidx) = qly1(kk) + qlvy1
  if qly1(qlidx) >= 100 and qlvy1 > 0 then qly1(qlidx)=99 : qlvy1 = -qlvy1
  if qly1(qlidx) < 0 and qlvy1 < 0 then qly1(qlidx)=0 : qlvy1 = -qlvy1

  qlx2(qlidx) = qlx2(kk) + qlvx2
  if qlx2(qlidx) >= 160 and qlvx2 > 0 then qlx2(qlidx)=159 : qlvx2 = -qlvx2
  if qlx2(qlidx) < 0 and qlvx2 < 0 then qlx2(qlidx)=0 : qlvx2 = -qlvx2

  qly2(qlidx) = qly2(kk) + qlvy2
  if qly2(qlidx) >= 100 and qlvy2 > 0 then qly2(qlidx)=99 : qlvy2 = -qlvy2
  if qly2(qlidx) < 0 and qlvy2 < 0 then qly2(qlidx)=0 : qlvy2 = -qlvy2

  qlcurclr = mod(qlcurclr + 1, 16)
  qlclr(qlidx) = qlcurclr

  return


'------------------
.draw_qix_triangles
'------------------
  sides = 3
  sqang = - PI / 2
  gosub draw_qix_poly
  return


'----------------
.draw_qix_squares
'----------------
  sides = 4
  sqang = - 3 * PI / 4
  gosub draw_qix_poly
  return


'-------------
.draw_qix_poly
'-------------
  ' init
  qlidx = 0
  sqinc = 2 * PI / sides

  qlcnt = 1
  t = 0
  pdist = 45
.tri_loop
  pdist = 22.5 - 22.5 * cos(t)
  centrex = 80 : centrey = 50

  pang = sqang
  qlx1(qlidx) = centrex + pdist * cos(pang)
  qly1(qlidx) = centrey + pdist * sin(pang)

  pang = pang + sqinc
  qlx2(qlidx) = centrex + pdist * cos(pang)
  qly2(qlidx) = centrey + pdist * sin(pang)

  pang = pang + sqinc
  qlx3(qlidx) = centrex + pdist * cos(pang)
  qly3(qlidx) = centrey + pdist * sin(pang)

  if sides = 4 then begin
    pang = pang + sqinc
    qlx4(qlidx) = centrex + pdist * cos(pang)
    qly4(qlidx) = centrey + pdist * sin(pang)
  bend
  
  qlcurclr = mod(qlcurclr + 1, 16)
  qlclr(qlidx) = qlcurclr

  vsync 0
  print "{x93}";

  for k = 0 to qlcnt - 1
    ' kk = mod(qlidx + qlcnt-1 - k, 5)
    kk = mod(qlidx + k, 5)

    sys $7e0c, qlclr(kk) + 64

    x1 = qlx1(kk)
    y1 = qly1(kk)
    x2 = qlx2(kk)
    y2 = qly2(kk)

    'cursor 0,0:print chr$(27);"qx1,y1,x2,y2=",int(x1),int(y1),int(x2),int(y2)
    'cursor 0,1:print chr$(27);"qvx1,vy1,vx2,vy2=";qlvx1;qlvy1;qlvx2;qlvy2
    gosub draw_line

    tx = x1
    ty = y1

    x1 = qlx3(kk)
    y1 = qly3(kk)
    gosub draw_line

    if sides = 3 then begin
      x2 = tx
      y2 = ty
      gosub draw_line
    bend:else begin  ' sides = 4
      x2 = qlx4(kk)
      y2 = qly4(kk)
      gosub draw_line

      x1 = tx
      y1 = ty
      gosub draw_line
    bend

    'cursor x1/2,y1/2:print "+";
    'cursor x2/2,y2/2:print "+";
    'get key a$
  next k

  ' calc next line positions
  kk = qlidx  ' old qlidx
  qlidx = mod(qlidx + 1, 5)
  if qlcnt < 5 then qlcnt = qlcnt + 1

  sqang = sqang + .1
  if sqang > 2*PI then sqang = sqang - 2*PI

  qlcurclr = mod(qlcurclr + 1, 16)
  qlclr(qlidx) = qlcurclr

  t = t + .1
  if t > 2*PI then t = t - 2*PI

  goto tri_loop

  return


'---------------
.draw_many_cubes
'---------------
  cb_incb = 0
  cb_inca = 0
  t1 = 0
  t2 = 0
  pdist = 45

.mc_loop
  centrex = 80 + 45*sin(t2)
  centrey = 50 + 15*sin(t1)

  pdist = 20 - 20 * cos(t1)
  cb_incb = PI/2 * sin(t1)
  cb_inca = PI/2 * sin(t2)
  t1 = t1 + .1
  t2 = t2 + .05
  if t1 > 2*PI then t1=t1-2*PI
  if t2 > 2*PI then t2=t2-2*PI

    print"{x93}"
    gosub draw_cube
  goto mc_loop

  return
  

'---------
.draw_cube
'---------
  cbidx = 0

  cb_angb = atn(1/sqr(2))
  for cb_anga = PI / 4 to 1.9 * PI step PI/2
    cbtmp = pdist * cos(cb_angb)
    pz = cbtmp * sin(cb_anga)
    py(cbidx) = pdist * sin(cb_angb)
    px(cbidx) = cbtmp * cos(cb_anga)
    gosub rotate_inca
    gosub rotate_incb
    px(cbidx) = centrex + px(cbidx) - pz * zfact / 4
    py(cbidx) = centrey + py(cbidx) + pz * zfact

    'print "px(";cbidx;")=";px(cbidx);", py(";cbidx;")=";py(cbidx)
    cbidx = cbidx + 1
  next cb_anga 

  plen = 4
  gosub draw_poly

  ' draw lines from poly
  cbidx = 0
  cb_angb = -atn(1/sqr(2))
  for cb_anga = PI / 4 to 1.9 * PI step PI/2
    x1 = px(cbidx)
    y1 = py(cbidx)

    cbtmp = pdist * cos(cb_angb)
    pz = cbtmp * sin(cb_anga)
    py(cbidx) = pdist * sin(cb_angb)
    px(cbidx) = cbtmp * cos(cb_anga)
    gosub rotate_inca
    gosub rotate_incb
    px(cbidx) = centrex + px(cbidx) - pz * zfact / 4
    py(cbidx) = centrey + py(cbidx) + pz * zfact

    x2 = px(cbidx)
    y2 = py(cbidx)

    gosub draw_line
    cbidx = cbidx + 1
  next cb_anga

  plen = 4
  gosub draw_poly

  return


'---------
.draw_poly
'---------
  for pidx = 0 to plen-2
    x1 = px(pidx) : y1 = py(pidx)
    x2 = px(pidx+1) : y2 = py(pidx+1)
    ' print "x1=";x1;", y1=";y1;", x2=";x2;", y2=";y2
    ' get key a$
    gosub draw_line
  next pidx

  x1 = px(plen-1) : y1 = py(plen-1)
  x2 = px(0) : y2 = py(0)
  gosub draw_line

  return


'-----------
.rotate_inca
'-----------
  ' rotate along y-axis by inca
  diffx = px(cbidx)
  diffy = pz
  cbtmp = sqr(diffx*diffx + diffy*diffy)
  gosub atan2  ' returns angle
  angle = angle + cb_inca
  px(cbidx) = cbtmp * cos(angle)
  pz = cbtmp * sin(angle) 
  return


'-----------
.rotate_incb
'-----------
  ' rotate along z-axis by incb
  diffx = px(cbidx)
  diffy = py(cbidx)
  cbtmp = sqr(diffx*diffx + diffy*diffy)
  gosub atan2  ' returns angle
  angle = angle + cb_incb
  px(cbidx) = cbtmp * cos(angle)
  py(cbidx) = cbtmp * sin(angle) 
  return


'-----
.atan2
'-----
  if diffx = 0 then begin
    if diffy<0 then angle = - PI/2
    if diffy>0 then angle = PI/2
  bend:else begin
    tanval = diffy / diffx
    angle = atn(tanval)  ' this is range -90deg to 90deg
    if diffx<0 then begin
     if diffy<0 then angle = angle - PI:else angle = angle + PI
    bend
  bend
  return


'---------
.draw_line
'---------
  bank 0
  sys $7e1b
  x1 = mod(int(x1)+256,256)
  y1 = mod(int(y1)+256,256)
  x2 = mod(int(x2)+256,256)
  y2 = mod(int(y2)+256,256)
  sys $7e21, x1, y1, x2, y2
  bank 128
  return


'----
.init
'----
  m = 0

  print "initialising..."
  gosub init_baslogo
  gosub load_data

  song$ = "enigma.p"
  gosub load_song

  for k = 0 to 20
    starx%(k) = int(rnd(1)*80)*256
    stary(k) = int(rnd(1)*50)
    starvx%(k) = (rnd(1)*4 + .5)*256
  next k

  ' clear the offscreen buffer
  edma 3, $fa0, 32, $50000
  edma 3, $fa0, 0, $51000

  edma 3, $fa0, 32, $52000
  edma 3, $fa0, 0, $53000

  edma 3, $fa0, 32, $54000
  edma 3, $fa0, 0, $55000

  fname$ = "ana.ln"
  fidx% = 0
  ver(fidx%) = 1
  gosub load_line_file

  fname$ = "silent.l"
  fidx% = 1
  ver(fidx%) = 2
  gosub load_line_file

  gosub init_writer
  gosub init_megaplot

  ' init var interface to assembly
  loc = $40036

  ptr = pointer(starx%(0))
  gosub store_ptr

  ptr = pointer(starvx%(0))
  gosub store_ptr
  return


'---------
.load_song
'---------
  dopen #2,(song$),r,u8

  ' load melody chunks
  ' ------------------
  input #2, chunk_cnt
  for chunk_idx = 0 to chunk_cnt - 1
    for vidx=0 to 5
      input #2,v$(vidx, chunk_idx)
    next vidx
  next chunk_idx

  input #2,tmpo%
  
  ' load envelopes
  ' --------------
  for row = 0 to 9
    input #2, tmp$
    input #2, tmp
    input #2, tmp
    input #2, tmp
    input #2, tmp
    input #2, tmp
    input #2, tmp
  next row
  print

  ' load filter presets
  ' -------------------
  for row = 0 to 10
    input #2, tmp$
    input #2, tmp
    input #2, tmp
    input #2, tmp
    input #2, tmp
    input #2, tmp
    input #2, tmp
    input #2, tmp
    input #2, tmp
  next row

  ' load sequence
  ' -------------
  seqcnt = 0
  input #2, seqcnt
  row = 0
  do while row < seqcnt
    input #2, seq_chunk(row)
    input #2, seq_extra(row)
    row = row + 1
  loop


  dclose #2

  chunk_idx = 0
  return


'--------------------
.parse_current_seqptr
'--------------------
  ret% = 0
  if plyptr = -1 then ret%=1:return
  if rplay(1)<>0 then ret%=1:return

  k = seq_chunk(plyptr)
  
  if k >= 0 then begin
    chunk_idx = k
    if chunk_rpt <= 0 then chunk_rpt = seq_extra(plyptr)
    gosub play_chunk_idx
    'sleep .1
    ret% = 1
  bend
  if k = CMD_TEMPO then begin
    k = seq_extra(plyptr)
    tempo (k)
    tmpo% = k
  bend
  if k = CMD_ECHO_M1 then echo_m = 1 : echo_delay = 0
  if k = CMD_ECHO_M2 then echo_m = 2 : echo_delay = 0
  if k = CMD_ECHO_B  then echo_b = 1 : echo_delay = 0
  if k = CMD_ECHO_OFF then echo_m = 0 : echo_b = 0
  if k = CMD_ECHO_SZ then echo_sz = seq_extra(plyptr)
  if k = CMD_DELAY then echo_delay = 1 : echo_m = 0 : echo_b = 0

  if k = CMD_VOL then v = seq_extra(plyptr) : vol v, v

  if chunk_rpt > 0 then chunk_rpt = chunk_rpt - 1

  if chunk_rpt <= 0 then plyptr = plyptr + 1

  if k = CMD_LOOP then plyptr = 0

  if plyptr >= seqcnt then plyflag = 0 : plyptr = -1
  return


'--------------
.play_chunk_idx
'--------------
  v1$ = v$(0,chunk_idx)
  v2$ = v$(1,chunk_idx)
  v3$ = v$(2,chunk_idx)
  v4$ = v$(3,chunk_idx)
  v5$ = v$(4,chunk_idx)
  v6$ = v$(5,chunk_idx)

  if echo_delay = 1 then begin
    play v1$
    sleep (echo_sz)
    play ,v2$
    sleep (echo_sz)
    play ,,v3$
    sleep (echo_sz)
    play ,,,v4$
    sleep (echo_sz)
    play ,,,,v5$
    sleep (echo_sz)
    play ,,,,,v6$
    sleep (echo_sz)
    return
  bend

  if echo_m = 0 and echo_b = 0 then begin
    play v1$, v2$, v3$, v4$, v5$, v6$
    return
  bend

  if echo_m = 1 and echo_b = 0 then begin
    play v1$, v2$, v3$,    , v5$, v6$
    sleep (echo_sz)
    play    ,    ,    , v1$
  bend

  if echo_m = 1 and echo_b = 1 then begin
    play v1$, v2$, v3$,    , v5$
    sleep (echo_sz)
    play    ,    ,    , v1$,    , v3$
  bend

  if echo_m = 2 and echo_b = 0 then begin
    play v1$,    , v3$,    ,    , v6$
    sleep (echo_sz)
    play    ,    ,    , v1$
    sleep (echo_sz)
    play    , v1$
    sleep (echo_sz)
    play    ,    ,    ,    , v1$
  bend

  if echo_m = 2 and echo_b = 1 then begin
    play v1$,    , v3$
    sleep (echo_sz)
    play    ,    ,    , v1$
    sleep (echo_sz)
    play    , v1$,    ,    ,    , v3$
    sleep (echo_sz)
    play    ,    ,    ,    , v1$
  bend

  return


'-------------
.init_megaplot
'-------------
  ' init
  qlidx = 0 : qlcnt = 0

  qlx1(qlidx) = int(rnd(1)*160)
  qly1(qlidx) = int(rnd(1)*100)

  qlx2(qlidx) = int(rnd(1)*160)
  qly2(qlidx) = int(rnd(1)*100)
  qlcnt = qlcnt + 1

  qlvx1 = rnd(1)*3 + 1
  qlvy1 = rnd(1)*3 + 1
  qlvx2 = rnd(1)*3 + 1
  qlvy2 = rnd(1)*3 + 1

  qlcurclr = mod(qlcurclr + 1, 16)
  qlclr(qlidx) = qlcurclr

  return


'-----------
.init_writer
'-----------
  txt_k = 0
  wxx = 0 : wyy = 0

  fname$ = "abc.ln"
  fidx% = 2
  ver(fidx%) = 3
  gosub load_line_file

  char_cnt = 0
  char_idx%(0) = 0
  char_width%(0) = 0
  max_width = 0

  k = 0
  do while k < didx%(fidx%)
    ' get x value
    val& = ldata&(fidx%, k)
    k=k+1

    if val& = $ff then begin
      ' keep an array of widths of each letter
      char_width%(char_cnt)=max_width

      ' print "char_width(";char_cnt;") = ";max_width
      ' get key z$

      max_width = 0
      char_cnt=char_cnt+1

      ' keep an array of indices to each drawn letter
      char_idx%(char_cnt) = k
      'print "char_idx%(";char_cnt;")=";k
      'get key a$
    bend

    if val& = $ff then goto cont_loop

    val& = val& and $7f
    if val& > max_width then max_width = val&
    
    k=k+1  ' skip y value
    k=k+1  ' skip char 

.cont_loop
  loop

  return


'---------------
.iter_write_text
'---------------
  if wc_flag = 0 and txt_k > 0 and txt_k <= len(s$) then begin
    chr = asc(mid$(s$, txt_k, 1))
    ' print "{x13}";chr$(27);"qchr=";chr;
    gosub petscii_to_screencode
    gosub screencode_to_idx
    ' print "fixed chr=";chr;
    ' get key z$

    if chr=32 then begin
      wxx=wxx+7
      txt_k = txt_k + 1
    bend:else:begin
      wc_flag = 1
      wc_chr = chr
      pidx%(fidx%) = char_idx%(chr)
    bend
  bend

  if wc_flag = 1 then begin
    chr = wc_chr
    gosub iter_write_char
    if ret = 1 then begin
      wxx = wxx + char_width%(wc_chr) + 1
      txt_k = txt_k + 1
      wc_flag = 0
    bend
  bend
  return


'----------
.iter_write_char
'---------------
  ' iterate over chars in string
    ' draw current char at given x,y pos
    ' increment x,y location

  ret = 1

  if pidx%(fidx%) < didx%(fidx%) and ldata&(fidx%, pidx%(fidx%)) <> $ff then begin

    ' cursor 0,49
    ' print chr$(27);"qpidx=";pidx%(fidx%);"[";ldata&(fidx%, pidx%);
    ' print ldata&(fidx%, pidx%+1);ldata&(fidx%, pidx%+2);

    val& = ldata&(fidx%, pidx%(fidx%))
    pidx%(fidx%) = pidx%(fidx%) + 1

    x = val& and $7f

    if val& and $80 then rev_flag&(fidx%) = 1 : else rev_flag&(fidx%) = 0
    y = ldata&(fidx%, pidx%(fidx%))
    pidx%(fidx%) = pidx%(fidx%) + 1

    a$ = chr$(ldata&(fidx%, pidx%(fidx%)))
    pidx%(fidx%) = pidx%(fidx%) + 1

    dxx = wxx+x : dyy = wyy+y : gosub draw_char

    ' print "{x13}{x11}";chr$(27);"q x,y,pidx,a$=";x;y;pidx%(fidx%),asc(a$)
    ' get key a$
    ret = 0
.pd_endloop
  bend

  return


'---------
.store_ptr
'---------
  wpoke loc, ptr
  loc = loc + 2
  return


'--------------
.load_line_file
'-------------- ' (fname$)
  if fname$ = "" then return
  gosub line_file_init
  dopen #2,(fname$),r,u8

  do
    get #2, val&
    if st then exit
    ldata&(fidx%, didx%(fidx%)) = val&
    didx%(fidx%) = didx%(fidx%) + 1
  loop

  dclose #2
  gosub play_data_init
  return


'--------------
.line_file_init
'--------------
  oldx = -1 : oldy = -1 : oldoffs(fidx%) = 0
  didx%(fidx%) = 0
  group_flag& = 0 : rev_flag&(fidx%) = 0
  offs(fidx%) = 0
  return


'--------------
.draw_starfield
'--------------
  for k = 0 to 20
    poke $50000 + starx%(k)/256 + stary(k) * 80, 46  ' dot
  next k
  return


'--------------
.iter_starfield
'--------------
  bank 0:sys $7e0f:bank 128

  'for k = 0 to 20
  '  starx(k) = mod( (starx(k)-starvx(k)) + 80, 80)
  'next k

  'cursor 0,0
  'for k = 0 to 20
  '  print int(starx%(k)/256), stary(k)
  'next k
  'get key a$
  return


'---------
.page_flip
'---------
  bank 0 : sys $7e15 : bank 128

  return


'--------------
.play_data_init
'--------------
  offs(fidx%)=0
  pidx%(fidx%) = 0
  if lastflag = 1 then pidx%(fidx%) = last_group_idx : lastflag = 0
  return

'--------------
.play_data_iter
'--------------
  if pidx%(fidx%) < didx%(fidx%) then begin
    val& = ldata&(fidx%, pidx%(fidx%))
    pidx%(fidx%) = pidx%(fidx%) + 1

    if ver(fidx%)=3 and val& = $ff then begin
      if pidx%(fidx%) > last_group_idx then last_group_idx = pidx%(fidx%)
      goto pd_endloop
    bend

    x = val& and $7f
    if ver(fidx%)=2 then begin
      x = val&
      val& = ldata&(fidx%, pidx%(fidx%))
      pidx%(fidx%) = pidx%(fidx%) + 1
      x = x + (val& and $7f) * 256
    bend

    if val& and $80 then rev_flag&(fidx%) = 1: else rev_flag&(fidx%) = 0
    y = ldata&(fidx%, pidx%(fidx%))
    pidx%(fidx%) = pidx%(fidx%) + 1

    a$ = chr$(ldata&(fidx%, pidx%(fidx%)))
    pidx%(fidx%) = pidx%(fidx%) + 1

    if ver(fidx%)=2 then begin
      if x-offs(fidx%) > 78 then begin
        gosub scroll_canvas_right
        pidx%(fidx%) = pidx%(fidx%) - 4
        return
        ' print "{x13}";chr$(27);"qoffs=";offs(fidx%);
      bend
    bend

    dxx = x-offs(fidx%) : dyy = y: gosub draw_char

    debug = 0
    if debug=1 then begin
      print "{x13}{x11}";chr$(27);"q x,y,pidx,a$,offs=";x;y;pidx%(fidx%),asc(a$),offs(fidx%)
      get key a$
    bend

.pd_endloop

  bend

  x = x-offs(fidx%)

  return


'---------
.draw_char
'---------
  ' poke $40800 + x + y*80, asc(a$)
  ' poke $ff80000 + x + y*80, rcolor(1)
  
  chr = asc(a$)
  gosub petscii_to_screencode

  if rev_flag&(fidx%) = 1 then chr = chr + 128

  if ver(fidx%) = 1 then begin
    poke $52000 + dxx + dyy*80, chr
    poke $53000 + dxx + dyy*80, 1
  bend

  if ver(fidx%) = 2 or ver(fidx%) = 3 then begin
    poke $54000 + dxx + dyy*80, chr
    poke $55000 + dxx + dyy*80, 2
  bend

  return


'---------------------
.petscii_to_screencode
'---------------------
  ' Based on the table here:
  ' https://sta.c64.org/cbm64pettoscr.html
  ' - seems like I needed to do a repair to it on the $a0-->$bf range
  if chr>=$00 and chr<=$1f then chr=chr or $80:return
  if chr>=$20 and chr<=$3f then return
  if chr>=$40 and chr<=$5f then chr=chr and $bf:return
  if chr>=$60 and chr<=$7f then chr=chr and $df:return
  if chr>=$80 and chr<=$9f then chr=chr or $40:return
  if chr>=$a0 and chr<=$bf then chr=(chr and $7f) or $40:return ' $bf:return
  if chr>=$c0 and chr<=$fe then chr=chr and $7f:return
  return


'-----------------
.screencode_to_idx
'-----------------
  if chr >= 65 and chr <= 90 then chr = chr - 65:return
  if chr = 46 then chr = 26:return ' full-stop
  if chr = 33 then chr = 27:return ' exclamation
  if chr = 44 then chr = 28:return ' comma (smiley)
  if chr = 47 then chr = 29:return ' forward slash (love heart)
  if chr = 35 then chr = 30:return ' # hash (dhufish)
  if chr >= 1 and chr <= 26 then chr = chr + 30
  return


'---------------
.transparent_dma
'---------------
  wpoke $40009, src_addr
  wpoke $4000c, dest_addr
  wpoke $40007, length
  poke  $40004, transp
  poke $d702, 4 ' dma list in bank 4
  poke $d701, $00 ' dma list msb
  poke $d705, $00 ' dma list lsb

  return


'------------------
.scroll_canvas_left
'------------------
  for yy = 0 to 49
    edma OP_COPY+DST_DEC+SRC_DEC, {x5F}
           79, $52000 + yy*80 + 78, $52000 + yy*80 + 79
    poke $52000 + yy*80, 32 
  next yy
  return


'-------------------
.scroll_canvas_right
'-------------------
  offs(fidx%) = offs(fidx%) + 1
  if fidx% = 0 then addr = $20  ' $52000
  if fidx% = 1 then addr = $40  ' $54000

  bank 0:sys $7e12, addr: bank 128

  return
ÿ