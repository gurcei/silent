#output "baslogo"

' BANK4 Memory usage
' ------------------
' 4.0000 DMA-LIST
' 4.0800 SCREEN MEMORY
' 4.2000 PETSCII GRAPHICS

' BANK5 Memory usage
' ------------------
' 5.0000 screen buffer
' 5.1000 color ram buffer

.defines
#declare k, ptr, tmp, max_frames, frm_idx, frmw, frmh, sz
#declare frame_addr(25), src_addr, dest_addr, length, transp
#declare frameidx, fx, fy, mirror, data_addr, frmx, frmy
#declare frmx1, frmy1, scraddr, clraddr, cdata_addr, yy
#declare inc(3), inc_dir(3), a$, x, y
#declare tempr, tempg, tempb, m
#declare logo_state
#declare flip_var, upper_nibble, lower_nibble
#declare clr1, clr2, tstart
#declare bas_state = 0
#declare frmy(3), orig_frmy(3), finish_cnt, bcnt

#define FRM_B = 0
#define FRM_A = 1
#define FRM_S = 2
#define FRM_BACKDROP = 3

#declare HOTREG_7 = $d05d
#define SCRNPTRLSB = $d060
#define SCRNPTRBNK = $d062


  ' palette
  ' -------
  #declare default_red&(32)
  #declare default_green&(32)
  #declare default_blue&(32)
  #declare curr_red&(32)
  #declare curr_green&(32)
  #declare curr_blue&(32)
  #declare fadeval, tempval


.offsets
#define OFFSET_DATA_SIZE  = 0
#define OFFSET_NUM_FRAMES = 2
#define OFFSET_FRAME0     = 3

#define FRM_OFFSET_X          = 0
#define FRM_OFFSET_Y          = 1
#define FRM_OFFSET_W          = 2
#define FRM_OFFSET_H          = 3
#define FRM_OFFSET_HITBOXES   = 4
#define FRM_OFFSET_HB_HEAD    = 4
#define FRM_OFFSET_HB_TORSO   = 8
#define FRM_OFFSET_HB_FEET    = 12
#define FRM_OFFSET_HB_ATTACK  = 16
#define FRM_OFFSET_DATA       = 20


'----
.main
'----
  gosub init
  gosub load_data

  gosub state_loop

  gosub animate_logo
  end

#define ST_FADE_TO_WHITE = 0
#define ST_DRAW_BACKDROP_ONLY = 1
#define ST_FADE_TO_DEFAULT = 2
#define ST_DROP_IN_B = 3

'----------
.state_loop
'----------
  if logo_state = ST_FADE_TO_WHITE then begin
    gosub fade_to_white
    logo_state = ST_DRAW_BACKDROP_ONLY

  bend
  
  if logo_state = ST_DRAW_BACKDROP_ONLY then begin
    bank 128
    poke $d020, 1, 1
    gosub init_80x50
    gosub draw_backdrop
    gosub page_flip
    gosub store_curr_colours
    gosub set_palette_for_backdrop
    fadeval=0
    m=0
    logo_state = ST_FADE_TO_DEFAULT

  bend
  
  if logo_state = ST_FADE_TO_DEFAULT then begin
    tstart = ti

    if fadeval <= 16 then begin
      if mod(m,3) = 0 then begin
        gosub fade_colours_to_defaults
        fadeval = fadeval + 1
      bend
    bend

    gosub cycle_alt_palette
    m = m + 1

    if m = 50 then logo_state = ST_DROP_IN_B

    do
    loop while (ti-tstart) < .1

  bend

  if logo_state = ST_DROP_IN_B then begin
    gosub animate_logo
    gosub cycle_alt_palette
    m = m + 1
  bend

  goto state_loop

  return


'----------
.init_80x50
'----------
  ' switch back to upper-case
  poke $d018, peek($d018) and $fd

  ' switch to 80x50
  setbit HOTREG_7, 7
  poke $d031, $e8 ' 1110 1000 = H640,FAST,ATTR,V400

  wpoke SCRNPTRLSB, $0800
  poke SCRNPTRBNK, $04

  return


'-------------
.draw_backdrop
'-------------
    frameidx = FRM_BACKDROP
    fx=-999 : fy = -999
    mirror = 0
    gosub draw_petscii_frame
    return


'------------
.animate_logo
'------------
  if bas_state = 0 then begin
    orig_frmy(0) = 6
    orig_frmy(1) = 23
    orig_frmy(2) = 6

    frmy(0) = -44
    frmy(1) = -69
    frmy(2) = -107
    bas_state = 1

    inc_dir(0) = 1
    inc(0) = 1

    inc_dir(1) = 1
    inc(1) = 1

    inc_dir(2) = 1
    inc(2) = 1

    inc_dir(3) = 1
    inc(3) = 1

  bend

  if bas_state = 1 then begin
    finish_cnt = 0
    for k = 0 to 2
      if frmy(k) < orig_frmy(k) then begin
        frmy(k) = frmy(k) + 2
        if frmy(k) > orig_frmy(k) then frmy(k) = orig_frmy(k)
      bend

      if frmy(k) = orig_frmy(k) then finish_cnt = finish_cnt or 2^k
    next k

  bend
  gosub draw_bas
  gosub page_flip

  for frameidx = 0 to 2
    if finish_cnt and 2^frameidx and bcnt(frameidx)<3 then begin
      inc(frameidx) = inc(frameidx) + .2 * inc_dir(frameidx)
      if inc(frameidx) < .6 then begin
        inc_dir(frameidx) = 1
        bcnt(frameidx)=bcnt(frameidx)+1
      bend
      if inc(frameidx) > 2.9 then begin
        inc_dir(frameidx) = -1
        bcnt(frameidx)=bcnt(frameidx)+1
      bend

      if bcnt(frameidx) = 2 and inc(frameidx) > 1 then begin
        inc(frameidx) = 1
        bcnt(frameidx)=bcnt(frameidx)+1
      bend
    bend

    bend
  next frameidx
  
  return

'----
.init
'----
  logo_state = 0

  gosub store_default_colours

  inc_dir(3) = -1
  inc(3) = 1

  ' clear dma list area

  for k = 0 to 20
    poke $40000+k, 0
  next k

  poke $40000, $81 ' opt = dest addr MB selector
  poke $40001, $00 ' default to 1st megabyte

  poke $40002, $07 ' opt = enable transparency
  poke $40003, $86 ' opt = set transparency
  poke $40004, $20 ' transparency value
  poke $40005, $00 ' end of options

  poke $40006, $00 ' CMD lsb = COPY
  wpoke $40007, $0000 ' length
  wpoke $40009, $1000 ' source addr
  poke $4000b, $05    ' source bank
  wpoke $4000c, $1000 ' dest addr
  poke $4000e, $05    ' dest bank
  poke $4000f, $00    ' CMD msb (ignore)
  wpoke $40010, $0000 ' modulo (ignore)

  ' clear the offscreen buffer
  edma 3, $fa0, 32, $50000
  edma 3, $fa0, 0, $51000
  return

'---------
.load_data
'---------
  ptr = $42000
  
  bload "bas.bin",p(ptr)
  gosub init_petscii_frames
  gosub _increment_ptr_to_next_free
  return

'-------------------
.init_petscii_frames
'-------------------
  tmp = ptr
  max_frames = peek(ptr + OFFSET_NUM_FRAMES)
  'print
  'print "ptr=";hex$(ptr + OFFSET_NUM_FRAMES)
  ptr = ptr + OFFSET_FRAME0

  'print
  'print "- mx_frm=";max_frames;": "

  frm_idx = 0
  do while frm_idx < max_frames
    'print frm_idx;
    frame_addr(frm_idx) = ptr
    frmx = peek(ptr + FRM_OFFSET_X)
    frmy = peek(ptr + FRM_OFFSET_Y)
    frmw = peek(ptr + FRM_OFFSET_W)
    frmh = peek(ptr + FRM_OFFSET_H)

    frmx1=frmx+frmw-1
    frmy1=frmy+frmh-1

    data_addr = ptr
    data_addr = data_addr + FRM_OFFSET_DATA
    cdata_addr = data_addr + frmw * frmh

    for y = 0 to frmy1-frmy
      for x = 0 to frmx1-frmx
        if peek(data_addr + x + y * frmw) = 102 then begin
          poke (cdata_addr + x + y * frmw), $ff
        bend
      next x
    next y
    
    'print "ptr=";hex$(ptr);", idx=";frm_idx;", w=";frmw;", h=";frmh
    ptr = ptr + FRM_OFFSET_DATA + frmw * frmh * 2

    'print "frm_idx=";frm_idx

    frm_idx = frm_idx + 1
  loop

  ptr = tmp
  return

'---------------------------
._increment_ptr_to_next_free
'---------------------------
  ptr = tmp
  sz = wpeek(ptr + OFFSET_DATA_SIZE)
  'print
  'print "inc sz = ";sz
  'print "ptr = ";hex$(ptr + OFFSET_DATA_SIZE)
  ptr = ptr + sz

  'get key a$
  return

'--------
.draw_bas
'--------
  frameidx = FRM_BACKDROP
  fx=-999 : fy = -999
  mirror = 0
  gosub draw_petscii_frame

  frameidx = FRM_B
  fx=-999 : fy=frmy(0)
  mirror = 0
  gosub draw_petscii_frame

  frameidx = FRM_A
  fx=-999 : fy=frmy(1)
  mirror = 0
  gosub draw_petscii_frame

  frameidx = FRM_S
  fx=-999 : fy=frmy(2)
  mirror = 0
  gosub draw_petscii_frame

  return


'------------------
.draw_petscii_frame
'------------------
  poke $4000b, $04  ' assure source bank 4 (for original frame data)
  if mirror = 1 then setbit $40006, 5:else clrbit $40006, 5
  data_addr = frame_addr(frameidx)
  frmx = peek(data_addr + FRM_OFFSET_X)
  frmy = peek(data_addr + FRM_OFFSET_Y)
  frmw = peek(data_addr + FRM_OFFSET_W)
  frmh = peek(data_addr + FRM_OFFSET_H)

  if fx<>-999 then frmx = fx ' override position
  if fy<>-999 then frmy = fy

  frmx1=frmx+frmw-1
  frmy1=frmy+frmh-1

  scraddr = $50000 + frmx + (frmy+frmh-1) * 80
  clraddr = $51000 + frmx + (frmy+frmh-1) * 80
  if mirror = 1 then clraddr = clraddr + frmw - 1

  data_addr = data_addr + FRM_OFFSET_DATA
  cdata_addr = data_addr + frmw * frmh

  for yy=frmy1 to frmy step -inc(frameidx)
    if yy < frmy then yy = frmy
    if scraddr < $50000 then goto for_skip

    ' draw chars
    ' ----------
    src_addr = data_addr + int(yy-frmy) * frmw
    dest_addr = scraddr
    length = frmw
    transp = 102
    gosub transparent_dma

    src_addr = cdata_addr + int(yy-frmy) * frmw
    dest_addr = clraddr
    length = frmw
    transp = $ff
    gosub transparent_dma

    'data_addr = d + frmw * int(frmy1 + yy
    'cdata_addr = cdata_addr + frmw
    scraddr = scraddr - 80
    clraddr = clraddr - 80
.for_skip
  next yy

  return


'---------------
.transparent_dma
'---------------
  wpoke $40009, src_addr
  wpoke $4000c, dest_addr
  wpoke $40007, length
  poke  $40004, transp
  poke $d702, 4 ' dma list in bank 4
  poke $d701, $00 ' dma list msb
  poke $d705, $00 ' dma list lsb

  return

'---------
.page_flip
'---------
  edma 0, $fa0, $50000, $40800
  edma 0, $fa0, $51000, $ff80000

  ' clear the offscreen buffer
  edma 3, $fa0, 32, $50000
  edma 3, $fa0, 0, $51000
  return


.fade_to_white
'-------------
  for fadeval=0 to 16
    gosub fade_colours_to_white
    sleep .1
  next fadeval
  return


.fade_colours_to_white
'---------------------
  'INPUT: fadeval=0 to 16
  tempval = fadeval / 16
  for k = 0 to 31

    flip_var = default_red&(k)
    gosub flip_nibble
    clr1 = flip_var

    clr2 = $f0

    flip_var = clr1 + (clr2 - clr1) * tempval
    gosub flip_nibble

    poke $d100+k, flip_var

    flip_var = default_green&(k)
    gosub flip_nibble
    clr1 = flip_var

    flip_var = clr1 + (clr2 - clr1) * tempval
    gosub flip_nibble

    poke $d200+k, flip_var

    flip_var = default_blue&(k)
    gosub flip_nibble
    clr1 = flip_var

    flip_var = clr1 + (clr2 - clr1) * tempval
    gosub flip_nibble

    poke $d300+k, flip_var
  next k
  return


.store_default_colours
'---------------------
  for k = 0 to 31
    default_red&(k)=peek($d100+k)
    default_green&(k)=peek($d200+k)
    default_blue&(k)=peek($d300+k)
  next k
  return


.fade_colours_to_defaults
'------------------------
  'INPUT: fadeval=0 to 16
  tempval = fadeval / 16
  for k = 0 to 31

    flip_var = curr_red&(k)
    gosub flip_nibble
    clr1 = flip_var

    flip_var = default_red&(k)
    gosub flip_nibble
    clr2 = flip_var

    flip_var = clr1 + (clr2 - clr1) * tempval
    gosub flip_nibble

    poke $d100+k, flip_var

    ' ------

    flip_var = curr_green&(k)
    gosub flip_nibble
    clr1 = flip_var

    flip_var = default_green&(k)
    gosub flip_nibble
    clr2 = flip_var

    flip_var = clr1 + (clr2 - clr1) * tempval
    gosub flip_nibble

    poke $d200+k, flip_var

    ' ------

    flip_var = curr_blue&(k)
    gosub flip_nibble
    clr1 = flip_var

    flip_var = default_blue&(k)
    gosub flip_nibble
    clr2 = flip_var

    flip_var = clr1 + (clr2 - clr1) * tempval
    gosub flip_nibble

    poke $d300+k, flip_var
  next k
  return


.store_curr_colours
'------------------
  for k = 0 to 31
    curr_red&(k)=peek($d100+k)
    curr_green&(k)=peek($d200+k)
    curr_blue&(k)=peek($d300+k)
  next k
  return


.cycle_alt_palette_loop
'----------------------
  for m = 0 to 32*4
    gosub cycle_alt_palette
    ' gosub poll_play
    ' gosub anim_draw_subtext
  next m
  return

.cycle_alt_palette
'-----------------
  tempr = peek($d100+12)
  tempg = peek($d200+12)
  tempb = peek($d300+12)
  for k = 12 to 30
    poke $d100+k, peek($d100+k+1)
    poke $d200+k, peek($d200+k+1)
    poke $d300+k, peek($d300+k+1)
  next k
  poke $d100+31, tempr
  poke $d200+31, tempg
  poke $d300+31, tempb

  tempr = curr_red&(12)
  tempg = curr_green&(12)
  tempb = curr_blue&(12)
  for k = 12 to 30
    curr_red&(k) = curr_red&(k+1)
    curr_green&(k) = curr_green&(k+1)
    curr_blue&(k) = curr_blue&(k+1)
  next k
  curr_red&(31) = tempr
  curr_green&(31) = tempg
  curr_blue&(31) = tempb

  tempr = default_red&(12)
  tempg = default_green&(12)
  tempb = default_blue&(12)
  for k = 12 to 30
    default_red&(k) = default_red&(k+1)
    default_green&(k) = default_green&(k+1)
    default_blue&(k) = default_blue&(k+1)
  next k
  default_red&(31) = tempr
  default_green&(31) = tempg
  default_blue&(31) = tempb

  return


'------------------------
.set_palette_for_backdrop
'------------------------
#define COL1_R = $da ' 218
#define COL1_G = $89 ' 137
#define COL1_B = $00 ' 0

#define COL2_R = $1b ' 27
#define COL2_G = $42 ' 66
#define COL2_B = $0b ' 11

  for k = 12 to 21
    flip_var = COL1_R + (COL2_R-COL1_R) * (k-12)/9
    gosub flip_nibble
    default_red&(k) = flip_var
    default_red&(43-k) = flip_var

    flip_var = COL1_G + (COL2_G-COL1_G) * (k-12)/9
    gosub flip_nibble
    default_green&(k) = flip_var
    default_green&(43-k) = flip_var

    flip_var = COL1_B + (COL2_B-COL1_B) * (k-12)/9
    gosub flip_nibble
    default_blue&(k) = flip_var
    default_blue&(43-k) = flip_var
  next k
  return


'-----------
.flip_nibble
'-----------
  lower_nibble = flip_var and $0f
  upper_nibble = (flip_var >> 4) and $0f

  flip_var = (lower_nibble << 4) + upper_nibble
  return
ÿ